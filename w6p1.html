<!DOCTYPE html>
<html>
<head>
    <title>W6P1</title>
    <script type="text/javascript" src="w6p1.js"></script>
    <script type="text/javascript" src="common/MV.js"></script>
    <script type="text/javascript" src="common/OBJParser.js"></script>
    <script type="text/javascript" src="common/BspTree.js"></script>
    <script type="text/javascript" src="common/Aabb.js"></script>
</head>
<body>
    <canvas id="webgpu-canvas" width="800" height="450">
        Please use a browser that supports HTML5 canvas.
    </canvas>
    <script id="wgsl" type="x-shader">
        //Define Uniforms struct
        struct Uniforms
        {
            aspect : f32,
            cam_constant : f32,
        };

        struct Aabb
        {
            min: vec4f,
            max: vec4f,
        };

        //@group(0) @binding(5) var<storage> materialEmission : array<vec4f>;
        //@group(0) @binding(6) var<storage> lightIndices : array<u32>;
        //@group(0) @binding(6) var<storage> matIndices : array<u32>;
        
        const MAX_LEVEL = 20u;
        const BSP_LEAF = 3u;
        var<private> branch_node: array<vec2u, MAX_LEVEL>;
        var<private> branch_ray: array<vec2f, MAX_LEVEL>;

        @group(0) @binding(0) var<uniform> uniforms : Uniforms;
        @group(0) @binding(1) var<uniform> aabb : Aabb;
        @group(0) @binding(2) var<storage> vPositions : array<vec3f>;
        @group(0) @binding(3) var<storage> meshFaces : array<vec3u>;
        @group(0) @binding(4) var<storage> vNormals : array<vec3f>;
        @group(0) @binding(5) var<storage> materialColorEmission : array<vec4f>;
        @group(0) @binding(6) var<storage> matIndices : array<u32>;
        @group(0) @binding(7) var<storage> treeIds : array<u32>;
        @group(0) @binding(8) var<storage> bspTree : array<vec4u>;
        @group(0) @binding(9) var<storage> bspPlanes : array<f32>;
        
        // Define VSOut struct
        struct VSOut
        {
            @builtin(position) position : vec4f,
            @location(0) coords : vec2f,
        }

        //Define Light struct
        struct Light
        {
            L_i: f32,
            w_i: vec3f,
            dist: f32,
        }

        // Define pi as a constant
        const PI: f32 = 3.14159265359;

        @vertex
        fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut
        {
            const pos = array<vec2f, 4>(vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
            var vsOut : VSOut;
            vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
            vsOut.coords = pos[VertexIndex];
            return vsOut;
        }
        // Define Ray struct
        struct Ray
        {
            origin : vec3f,
            direction : vec3f,
            t_min : f32,
            t_max : f32,
        }

        //Define HitInfo struct
        struct HitInfo
        {
            has_hit : bool,
            dist : f32,
            position : vec3f,
            normal : vec3f,
            color : vec3f,
            depth : u32,
            shader : u32,
        };

        struct Onb
        {
            tangent: vec3f,
            binormal: vec3f,
            normal: vec3f,
        };
        

        fn get_camera_ray(uv : vec2f) -> Ray
        {
            var p = vec3f(-0.02, 0.11, 0.0);
            var up_vec = vec3f(0.0, 1.0, 0.0);
            var r : Ray;
            //r.origin = vec3f(0.15, 1.5 , 10.0);
            r.origin = vec3f(-0.02, 0.11, 0.6);
            var v = normalize(p - r.origin);
            var b1 = normalize(cross(v, up_vec));
            var b2 = cross(b1, v);
            var q = b1 * uv.x + b2 * uv.y + v * uniforms.cam_constant;
            r.direction = normalize(q); 
            r.t_min = 0.0;
            r.t_max = 1.0e32;
            return r;
        }

        fn intersect_min_max(r: ptr<function, Ray>) -> bool
        {
            var tmin = 1.0e32f;   
            var tmax = -1.0e32f;   
            for (var i = 0u; i < 3u; i++)
            {
                if(abs((*r).direction[i]) > 1.0e-8f)
                {
                    let p1 = (aabb.min[i] - (*r).origin[i]) / (*r).direction[i];
                    let p2 = (aabb.max[i] - (*r).origin[i]) / (*r).direction[i];
                    let pmin = min(p1, p2);
                    let pmax = max(p1, p2);
                    tmin = min(tmin, pmin);
                    tmax = max(tmax, pmax);
                }
            }
            if (tmin > tmax || tmin > (*r).t_max || tmax < (*r).t_min)
            {
                return false;
            }
            (*r).t_min = max(tmin - 1.0e4f, (*r).t_min);
            (*r).t_max = min(tmax + 1.0e4f, (*r).t_max);
            return true;
        }

        fn intersect_trimesh(r: ptr<function, Ray>, hit:ptr<function, HitInfo>, idx:ptr<function, Idx>) -> bool
        {
            var branch_lvl = 0u;
            var near_node = 0u;
            var far_node = 0u;
            var t = 0.0f;
            var node = 0u;
            for (var i = 0u; i <= MAX_LEVEL; i++)
            {
                let tree_node = bspTree[node];
                let node_axis_leaf = tree_node.x&3u;
                if (node_axis_leaf == BSP_LEAF)
                {
                    let node_count = tree_node.x>>2u;
                    let node_id = tree_node.y;
                    var found = false;
                    for (var j = 0u; j < node_count; j++)
                    {
                        let obj_idx = treeIds[node_id + j];
                        if(intersect_triangle(*r, hit, obj_idx))
                        {
                            (*r).t_max = (*hit).dist;
                            (*idx).i = obj_idx;
                            found = true;
                        }
                    }
                    if (found) { return true; }
                    else if (branch_lvl == 0u) { return false; }
                    else
                    {
                        branch_lvl--;
                        i = branch_node[branch_lvl].x;
                        node = branch_node[branch_lvl].y;
                        (*r).t_min = branch_ray[branch_lvl].x;
                        (*r).t_max = branch_ray[branch_lvl].y;
                        continue;
                    }
                }
                let axis_direction = (*r).direction[node_axis_leaf];
                let axis_origin = (*r).origin[node_axis_leaf];
                if (axis_direction >= 0.0f)
                {
                    near_node = tree_node.z;
                    far_node = tree_node.w;
                }
                else
                {
                    near_node = tree_node.w;
                    far_node = tree_node.z;
                }
                let node_plane = bspPlanes[node];
                let denom = select(axis_direction, 1.0e-8f, abs(axis_direction) < 1.0e-8f);
                t = (node_plane - axis_origin) / denom;
                if (t > (*r).t_max) { node = near_node; }
                else if(t < (*r).t_min) { node = far_node; }
                else
                {
                    branch_node[branch_lvl].x = i;
                    branch_node[branch_lvl].y = far_node;
                    branch_ray[branch_lvl].x = t;
                    branch_ray[branch_lvl].y = (*r).t_max;
                    branch_lvl++;
                    (*r).t_max = t;
                    node = near_node;
                }
            }   

            return false;
        }

        fn intersect_triangle(r: Ray, hit: ptr<function, HitInfo>, f_index: u32) -> bool
        {
            var f = meshFaces[f_index];
            var v0 = vPositions[f.x];
            var v1 = vPositions[f.y];
            var v2 = vPositions[f.z];
            var n0 = vNormals[f.x];
            var n1 = vNormals[f.y];
            var n2 = vNormals[f.z];

            var e0 = v1 - v0;
            var e1 = v2 - v0;
            var normal = cross(e0, e1);
            if((abs(dot(r.direction, normal))) < 1e-8)
            {
                return false;
            }
            var beta = dot(cross(v0 - r.origin, r.direction), e1) / dot(r.direction, normal);
            var gamma = - dot(cross(v0 - r.origin, r.direction), e0) / dot(r.direction, normal);
            if(beta >= 0 && gamma >= 0 && beta + gamma <= 1)
            {
                var t_prime = dot(v0 - r.origin, normal) / dot(r.direction, normal);
                if(t_prime > r.t_min && t_prime < r.t_max)
                {
                    (*hit).has_hit = true;
                    (*hit).dist = t_prime;
                    (*hit).position = r.origin + t_prime * r.direction;
                    //(*hit).normal = normalize(normal);
                    (*hit).normal = normalize((1 - beta - gamma) * n0 + beta * n1 + gamma * n2);
                    return true;
                }
            }
            return false;
        }

        struct Idx
        {
            i: u32,
        };

        fn intersect_scene(r : ptr<function, Ray>, hit : ptr<function, HitInfo>) -> bool
        {
            var idx : Idx;
            if(intersect_trimesh(r, hit, &idx)) 
            { 
                //(*hit).color = vec3f(0.9, 0.9, 0.9);
                (*hit).color = materialColorEmission[matIndices[idx.i]].xyz;
                (*hit).shader = 1;
            }

            //for(var c: u32 = 0; c < arrayLength(&meshFaces); c++)
            //{
            //    if(intersect_triangle((*r), hit, c))
            //    {
            //        //(*hit).color = materialColorEmission[matIndices[c]].xyz;
            //        //(*hit).color = materialColorEmission[c].xyz;
            //        (*hit).shader = 1;
            //        (*r).t_max = (*hit).dist;
            //    }
            //}
            return (*hit).has_hit;
        }

        fn sample_point_light(pos: vec3f) -> Light
        {
            var light : Light;
            var light_pos = vec3f(0.0, 1.0, 0.0);
            light.L_i = PI / dot(light_pos - pos, light_pos - pos);
            light.w_i = normalize(light_pos - pos);
            light.dist = length(light_pos - pos);
            return light;
        }

        fn sample_directional_light(pos : vec3f) -> Light
        {
            var dir_light : Light;
            dir_light.L_i = 1.5;
            dir_light.w_i = -normalize(vec3f(-1.0, -1.0, -1.0));
            dir_light.dist = 1.0e8;
            return dir_light;
        }

        fn calculate_area(v1 : vec3f, v2 : vec3f, v3 : vec3f) -> f32
        {
            var AB = vec3f(v2.x - v1.x, v2.y - v1.y, v2.z - v1.z);
            var AC = vec3f(v3.x - v1.x, v3.y - v1.y, v3.z - v1.z);
            return 0.5 * length(cross(AB, AC));
        }
        
        fn calculate_normal_area(v1 : vec3f, v2 : vec3f, v3 : vec3f) -> vec3f
        {
            var AB = vec3f(v2.x - v1.x, v2.y - v1.y, v2.z - v1.z);
            var AC = vec3f(v3.x - v1.x, v3.y - v1.y, v3.z - v1.z);
            return 0.5 * cross(AB, AC);
        }

        fn calculate_center(v1 : vec3f, v2 : vec3f, v3 : vec3f) -> vec3f
        {
            var x = (v1.x + v2.x + v3.x) / 3;
            var y = (v1.y + v2.y + v3.y) / 3;
            var z = (v1.z + v2.z + v3.z) / 3;
            return vec3f(x, y, z);
        }

        //fn sample_area_light(pos : vec3f) -> Light
        //{
        //    var area_light : Light;
        //    var L_e = 30.0;
        //    var light_pos = vec3f(0.0);
        //    for(var c: u32 = 0; c < arrayLength(&lightIndices); c++)
        //    {
        //        var lightIndex = lightIndices[c];
        //        var f = meshFaces[lightIndex];
        //        let center = calculate_center(vPositions[f.x], vPositions[f.y], vPositions[f.z]);
        //        light_pos += center;
        //    }
        //    light_pos /= f32(arrayLength(&lightIndices));
        //    area_light.w_i = (light_pos - pos);
        //
        //    for(var c: u32 = 0; c < arrayLength(&lightIndices); c++)
        //    {
        //        var lightIndex = lightIndices[c];
        //        var f = meshFaces[lightIndex];
        //        let emission = materialEmission[matIndices[lightIndex]];
        //        area_light.L_i += max(dot(-normalize(area_light.w_i), calculate_normal_area(vPositions[f.x], vPositions[f.y], vPositions[f.z])), 0) * L_e;
        //    }
        //    var r = length(light_pos - pos);
        //    area_light.L_i /= pow(r, 2);
        //    //area_light.dist = 1.0e8;
        //    area_light.dist = length(area_light.w_i);
        //    area_light.w_i = normalize(area_light.w_i);
        //    return area_light;
        //}

        fn lambertian(r: ptr<function,Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
            //var light : Light = sample_point_light((*hit).position);
            var light : Light = sample_directional_light((*hit).position);
            //var light : Light = sample_area_light((*hit).position);
            var L_a = (*hit).color * 0.1;  //ambient light
            var ro = (*hit).color * 0.9;
            var L_r = ro * light.L_i * max(dot(light.w_i, (*hit).normal), 0.0) / PI ;

            var shadow_ray = Ray((*hit).position, light.w_i, 1.0e-3, light.dist);

            var shadow_hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 1);
            if(intersect_scene(&shadow_ray, &shadow_hit))
            {
                return L_a;
            }
            else
            {
                return L_r + L_a;
            }
        }

        fn shade(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
            switch (*hit).shader
            {
                case 0 {
                    return (*hit).color;
                }
                case 1{
                    return lambertian(r, hit);
                }
                case default {return (*hit).color;}
            }
        }

        @fragment
        fn main_fs(@location(0) coords : vec2f) -> @location(0) vec4f
        {
            const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
            const max_depth = 10;
            var result = vec3f(0.0);
            var uv = vec2f(coords.x*uniforms.aspect*0.5, coords.y*0.5);
            var r = get_camera_ray(uv);
            var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 1);
            for(var i = 0; i < max_depth; i++)
            {
                if (!intersect_min_max(&r)) { result += bgcolor.rgb; break; }
                if(intersect_scene(&r, &hit)) { result += shade(&r, &hit); }
                else { result += bgcolor.rgb; break; }
                if(hit.has_hit) { break; }
            }
            return vec4f(result, bgcolor.a);
        }
    </script>
</body>
</html> 