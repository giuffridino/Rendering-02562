<!DOCTYPE html>
<html>
<head>
    <title>W5P2</title>
    <script type="text/javascript" src="w5p2.js"></script>
    <script type="text/javascript" src="common/MV.js"></script>
    <script type="text/javascript" src="common/OBJParser.js"></script>
</head>
<body>
    <canvas id="webgpu-canvas" width="800" height="450">
        Please use a browser that supports HTML5 canvas.
    </canvas>
    <script id="wgsl" type="x-shader">
        //Define Uniforms struct
        struct Uniforms
        {
            aspect : f32,
            cam_constant : f32,
            subdivs : f32,
        };

        @group(0) @binding(0) var<uniform> uniforms : Uniforms;
        @group(0) @binding(1) var<storage> vPositions : array<vec3f>;
        @group(0) @binding(2) var<storage> meshFaces : array<vec3u>;
        
        // Define VSOut struct
        struct VSOut
        {
            @builtin(position) position : vec4f,
            @location(0) coords : vec2f,
        }

        //Define Light struct
        struct Light
        {
            L_i: f32,
            w_i: vec3f,
            dist: f32,
        }

        // Define pi as a constant
        const PI: f32 = 3.14159265359;

        @vertex
        fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut
        {
            const pos = array<vec2f, 4>(vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
            var vsOut : VSOut;
            vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
            vsOut.coords = pos[VertexIndex];
            return vsOut;
        }
        // Define Ray struct
        struct Ray
        {
            origin : vec3f,
            direction : vec3f,
            t_min : f32,
            t_max : f32,
        }

        //Define HitInfo struct
        struct HitInfo
        {
            has_hit : bool,
            dist : f32,
            position : vec3f,
            normal : vec3f,
            color : vec3f,
            depth : u32,
            shader : u32,
        };

        struct Onb
        {
            tangent: vec3f,
            binormal: vec3f,
            normal: vec3f,
        };
        

        fn get_camera_ray(uv : vec2f) -> Ray
        {
            var p = vec3f(0.15, 1.5, 0.0);
            var up_vec = vec3f(0.0, 1.0, 0.0);
            var r : Ray;
            r.origin = vec3f(0.15, 1.5, 10.0);
            var v = normalize(p - r.origin);
            var b1 = normalize(cross(v, up_vec));
            var b2 = cross(b1, v);
            var q = b1 * uv.x + b2 * uv.y + v * uniforms.cam_constant;
            r.direction = normalize(q); 
            r.t_min = 0.0;
            r.t_max = 1.0e32;
            return r;
        }

        fn intersect_triangle(r: Ray, hit: ptr<function, HitInfo>, f_index: u32) -> bool
        {
            var f = meshFaces[f_index];
            var v0 = vPositions[f.x];
            var v1 = vPositions[f.y];
            var v2 = vPositions[f.z];

            var e0 = v1 - v0;
            var e1 = v2 - v0;
            var normal = cross(e0, e1);
            if((abs(dot(r.direction, normal))) < 1e-8)
            {
                return false;
            }
            var beta = dot(cross(v0 - r.origin, r.direction), e1) / dot(r.direction, normal);
            var gamma = - dot(cross(v0 - r.origin, r.direction), e0) / dot(r.direction, normal);
            if(beta >= 0 && gamma >= 0 && beta + gamma <= 1)
            {
                var t_prime = dot(v0 - r.origin, normal) / dot(r.direction, normal);
                if(t_prime > r.t_min && t_prime < r.t_max)
                {
                    (*hit).has_hit = true;
                    (*hit).dist = t_prime;
                    (*hit).position = r.origin + t_prime * r.direction;
                    (*hit).normal = normalize(normal);
                    return true;
                }
            }
            return false;
        }

        fn intersect_scene(r : ptr<function, Ray>, hit : ptr<function, HitInfo>) -> bool
        {
            for(var c: u32 = 0; c < arrayLength(&meshFaces); c++)
            {
                if(intersect_triangle((*r), hit, c))
                {
                    (*hit).color = vec3f(0.9, 0.9, 0.9);
                    (*hit).shader = 1;
                    (*r).t_max = (*hit).dist;
                }
            }
            return (*hit).has_hit;
        }

        fn sample_point_light(pos: vec3f) -> Light
        {
            var light : Light;
            var light_pos = vec3f(0.0, 1.0, 0.0);
            light.L_i = PI / dot(light_pos - pos, light_pos - pos);
            light.w_i = normalize(light_pos - pos);
            light.dist = length(light_pos - pos);
            return light;
        }

        fn sample_directional_light(pos : vec3f) -> Light
        {
            var dir_light : Light;
            //var light_pos = vec3f(0.0, -1.0, 0.0);
            dir_light.L_i = 3;
            dir_light.w_i = normalize(vec3f(-1.0));
            dir_light.dist = 0.0;

            return dir_light;
        }

        fn lambertian(r: ptr<function,Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
            //var light : Light = sample_point_light((*hit).position);
            var light : Light = sample_directional_light((*hit).position);
            var L_a = (*hit).color * 0.1;  //ambient light
            var ro = (*hit).color * 0.9;
            //var L_r = ro * light.L_i * dot((*hit).normal, light.w_i) / PI;
            var L_r = ro * light.L_i * dot((*hit).normal, -light.w_i) / PI;

            var shadow_ray = Ray((*hit).position, light.w_i, 1.0e-4, light.dist);

            var shadow_hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 1);
            if(intersect_scene(&shadow_ray, &shadow_hit))
            {
                return L_a;
            }
            else
            {
                return L_r + L_a;
            }
        }

        fn shade(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
            switch (*hit).shader
            {
                case 0 {
                    return (*hit).color;
                }
                case 1{
                    return lambertian(r, hit);
                }
                case default {return (*hit).color;}
            }
        }

        @fragment
        fn main_fs(@location(0) coords : vec2f) -> @location(0) vec4f
        {
            const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
            const max_depth = 10;
            var result = vec3f(0.0);
            var uv = vec2f(coords.x*uniforms.aspect*0.5, coords.y*0.5);
            var r = get_camera_ray(uv);
            var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 1);
            for(var i = 0; i < max_depth; i++)
            {
                if(intersect_scene(&r, &hit)) {result += shade(&r, &hit);}
                else { result += bgcolor.rgb; break;}
                if(hit.has_hit) { break;}
            }
            return vec4f(result, bgcolor.a);
        }
    </script>
    <input type = "range" id = "sliderCamera" min = "0.1" max = "2.0" step = "0.01" value = "1.0"> 
    </select>
    <select id = "subdivsMenu" size = "1">
        <option value = "2.0"> 2 subdivisions </option>
        <option value = "4.0"> 4 subdivisions</option>
        <option value = "8.0"> 8 subdivisions</option>
        <option value = "16.0"> 16 subdivisions</option>
    </select>
</body>
</html> 