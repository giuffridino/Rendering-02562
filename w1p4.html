<!DOCTYPE html>
<html>
<head>
    <title>W1P4</title>
    <script type="text/javascript" src="w1p4.js"></script>
    <script type="text/javascript" src="common/MV.js"></script>
</head>
<body>
    <canvas id="webgpu-canvas" width="512" height="512">
        Please use a browser that supports HTML5 canvas.
    </canvas>
    <script id="wgsl" type="x-shader">
        struct Uniforms
        {
            aspect : f32,
            cam_constant : f32,
            gamme : f32,
        };
        @group(0) @binding(0) var<uniform> uniforms : Uniforms;
        
        // Define VSOut struct
        struct VSOut
        {
            @builtin(position) position : vec4f,
            @location(0) coords : vec2f,
        }

        @vertex
        fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut
        {
            const pos = array<vec2f, 4>(vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
            var vsOut : VSOut;
            vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
            vsOut.coords = pos[VertexIndex];
            return vsOut;
        }
        // Define Ray struct
        struct Ray
        {
            origin : vec3f,
            direction : vec3f,
            t_min : f32,
            t_max : f32,
        }

        //Define HitInfo struct
        struct HitInfo
        {
            has_hit : bool,
            dist : f32,
            position : vec3f,
            normal : vec3f,
            color : vec3f,
        };

        fn get_camera_ray(uv : vec2f) -> Ray
        {
            var cam_const = 1.0;
            var p = vec3f(0.0, 0.5, 0.0);
            var up_vec = vec3f(0.0, 1.0, 0.0);
            var r : Ray;
            r.origin = vec3f(2.0, 1.5, 2.0);
            var v = normalize(p - r.origin);
            var b1 = normalize(cross(v, up_vec));
            var b2 = cross(b1, v);
            var q = b1 * uv.x + b2 * uv.y + v * cam_const;
            r.direction = normalize(q); 
            r.t_min = 0.0;
            r.t_max = 1.0e32;
            return r;
        }

        fn intersect_plane(r: Ray, hit: ptr<function, HitInfo>, position: vec3f, normal: vec3f) -> bool 
        {
            if((abs(dot(r.direction, normal))) < 1e-8)
            {
                return false;
            }
            var t_prime = dot((position - r.origin), normal) / dot(r.direction, normal);
            if(t_prime > r.t_min && t_prime < r.t_max)
            {
                (*hit).has_hit = true;
                (*hit).dist = t_prime;
                (*hit).position = r.origin + t_prime * r.direction;
                (*hit).normal = normal;
                return true;
            }
            return false;
        }

        fn intersect_triangle(r: Ray, hit: ptr<function, HitInfo>, v: array<vec3f, 3>) -> bool
        {
            var e0 = v[1] - v[0];
            var e1 = v[2] - v[0];
            var normal = cross(e0, e1);
            if((abs(dot(r.direction, normal))) < 1e-8)
            {
                return false;
            }
            var beta = dot(cross(v[0] - r.origin, r.direction), e1) / dot(r.direction, normal);
            var gamma = - dot(cross(v[0] - r.origin, r.direction), e0) / dot(r.direction, normal);
            if(beta >= 0 && gamma >= 0 && beta + gamma <= 1)
            {
                var t_prime = dot(v[0] - r.origin, normal) / dot(r.direction, normal);
                (*hit).has_hit = true;
                (*hit).dist = t_prime;
                (*hit).position = r.origin + t_prime * r.direction;
                (*hit).normal = normalize(normal);
                return true;
            }
            return false;
        }

        fn intersect_sphere(r: Ray, hit: ptr<function, HitInfo>, center: vec3f, radius: f32) -> bool
        {
            var b_half = dot((r.origin - center), r.direction);
            var c = dot(r.origin - center, r.origin - center) - pow(radius, 2);
            var temp = pow(b_half, 2) - c;
            if(temp < 0.0f)
            {
                return false;
            }
            var t1 = -b_half - sqrt(pow(b_half, 2) - c);
            if(t1 > r.t_min && t1 < r.t_max)
            {
                (*hit).has_hit = true;
                (*hit).dist = t1;
                (*hit).position = r.origin + t1 * r.direction;
                (*hit).normal = normalize((*hit).position - center);
                return true;
            }
            var t2 = -b_half + sqrt(pow(b_half, 2) - c);
            if(t2 > r.t_min && t2 < r.t_max)
            {
                (*hit).has_hit = true;
                (*hit).dist = t2;
                (*hit).position = r.origin + t2 * r.direction;
                (*hit).normal = normalize((*hit).position - center);
                return true;
            }
            return false;
        }

        fn intersect_scene(r : ptr<function, Ray>, hit : ptr<function, HitInfo>) -> bool
        {
            if(intersect_plane((*r), hit, vec3f(0.0), vec3f(0.0, 1.0, 0.0)))
            {
                (*hit).color = vec3f(0.1, 0.7, 0.0);
                (*r).t_max = (*hit).dist;
            }
            if(intersect_triangle((*r), hit, array<vec3f,3>(vec3f(-0.2, 0.1, 0.9), vec3f(0.2, 0.1, 0.9), vec3f(-0.2, 0.1, -0.1))))
            {
                (*hit).color = vec3f(0.4, 0.3, 0.2);
                (*r).t_max = (*hit).dist;
            }
            if(intersect_sphere((*r), hit, vec3f(0.0, 0.5, 0.0), 0.3))
            {
                (*hit).color = vec3f(0.0, 0.0, 0.0);
                (*r).t_max = (*hit).dist;
            }

            return (*hit).has_hit;
        }

        //fn lambertian(r: Ray, ro: f32)
        //{

        //}

        //fn shade(r: ptr<function, Ray>, hit: ptr<function, hitInfo>) -> vec3f
        //{
        //    switch (*hit).shader
        //    {
        //        case 1 {return lambertian(r, hit);}
        //        case default {return (*hit).color;}
        //    }
        //}

        @fragment
        fn main_fs(@location(0) coords : vec2f) -> @location(0) vec4f
        {
            const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
            const max_depth = 10;
            let uv = vec2f(coords.x*uniforms.aspect*0.5, coords.y*0.5);
            var r = get_camera_ray(uv);
            var result = vec3f(0.0);
            var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0));
            for(var i = 0; i < max_depth; i++)
            {
                if(intersect_scene(&r, &hit)) {result += hit.color; }
                else { result += bgcolor.rgb; break;}
                if(hit.has_hit) { break;}
            }
            return vec4f(result, bgcolor.a);
        }
    </script>
</body>
</html>