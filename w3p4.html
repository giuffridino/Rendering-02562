<!DOCTYPE html>
<html>
<head>
    <title>W3P4</title>
    <script type="text/javascript" src="w3p4.js"></script>
    <script type="text/javascript" src="common/MV.js"></script>
</head>
<body>
    <canvas id="webgpu-canvas" width="512" height="512">
        Please use a browser that supports HTML5 canvas.
    </canvas>
    <script id="wgsl" type="x-shader">
        //Define Uniforms struct
        struct Uniforms
        {
            aspect : f32,
            cam_constant : f32,
            shader : f32,
            subdivs : f32,
        };

        @group(0) @binding(0) var<uniform> uniforms : Uniforms;
        @group(0) @binding(1) var my_sampler : sampler;
        @group(0) @binding(2) var my_texture : texture_2d<f32>;
        @group(0) @binding(3) var<storage> jitter : array<vec2f>;
        
        // Define VSOut struct
        struct VSOut
        {
            @builtin(position) position : vec4f,
            @location(0) coords : vec2f,
        }

        //Define Light struct
        struct Light
        {
            L_i: f32,
            w_i: vec3f,
            dist: f32,
        }

        // Define pi as a constant
        const PI: f32 = 3.14159265359;

        @vertex
        fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut
        {
            const pos = array<vec2f, 4>(vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
            var vsOut : VSOut;
            vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
            vsOut.coords = pos[VertexIndex];
            return vsOut;
        }
        // Define Ray struct
        struct Ray
        {
            origin : vec3f,
            direction : vec3f,
            t_min : f32,
            t_max : f32,
        }

        //Define HitInfo struct
        struct HitInfo
        {
            has_hit : bool,
            dist : f32,
            position : vec3f,
            normal : vec3f,
            color : vec3f,
            shader : u32,
            sphere : bool,
            depth : u32,
            ior1_over_ior2 : f32,
            cos_sq_t : f32,
            specular : f32,
            shininess : f32,
            text_coords: vec2f,
            use_texture: bool,
        };

        struct Onb
        {
            tangent: vec3f,
            binormal: vec3f,
            normal: vec3f,
        };
        

        fn get_camera_ray(uv : vec2f) -> Ray
        {
            var p = vec3f(0.0, 0.5, 0.0);
            var up_vec = vec3f(0.0, 1.0, 0.0);
            var r : Ray;
            r.origin = vec3f(2.0, 1.5, 2.0);
            var v = normalize(p - r.origin);
            var b1 = normalize(cross(v, up_vec));
            var b2 = cross(b1, v);
            var q = b1 * uv.x + b2 * uv.y + v *uniforms.cam_constant;
            r.direction = normalize(q); 
            r.t_min = 0.0;
            r.t_max = 1.0e32;
            return r;
        }

        fn intersect_plane(r: Ray, hit: ptr<function, HitInfo>, position: vec3f, normal: vec3f) -> bool 
        {
            if((abs(dot(r.direction, normal))) < 1e-8)
            {
                return false;
            }
            var t_prime = dot((position - r.origin), normal) / dot(r.direction, normal);
            if(t_prime > r.t_min && t_prime < r.t_max)
            {
                (*hit).has_hit = true;
                (*hit).dist = t_prime;
                (*hit).position = r.origin + t_prime * r.direction;
                (*hit).normal = normal;
                return true;
            }
            return false;
        }

        fn intersect_triangle(r: Ray, hit: ptr<function, HitInfo>, v: array<vec3f, 3>) -> bool
        {
            var e0 = v[1] - v[0];
            var e1 = v[2] - v[0];
            var normal = cross(e0, e1);
            if((abs(dot(r.direction, normal))) < 1e-8)
            {
                return false;
            }
            var beta = dot(cross(v[0] - r.origin, r.direction), e1) / dot(r.direction, normal);
            var gamma = - dot(cross(v[0] - r.origin, r.direction), e0) / dot(r.direction, normal);
            if(beta >= 0 && gamma >= 0 && beta + gamma <= 1)
            {
                var t_prime = dot(v[0] - r.origin, normal) / dot(r.direction, normal);
                if(t_prime > r.t_min && t_prime < r.t_max)
                {
                    (*hit).has_hit = true;
                    (*hit).dist = t_prime;
                    (*hit).position = r.origin + t_prime * r.direction;
                    (*hit).normal = normalize(normal);
                    return true;
                }
            }
            return false;
        }

        fn intersect_sphere(r: Ray, hit: ptr<function, HitInfo>, center: vec3f, radius: f32) -> bool
        {
            var b_half = dot((r.origin - center), r.direction);
            var c = dot(r.origin - center, r.origin - center) - pow(radius, 2);
            var temp = pow(b_half, 2) - c;
            if(temp < 0.0f)
            {
                return false;
            }
            var t1 = -b_half - sqrt(pow(b_half, 2) - c);
            if(t1 > r.t_min && t1 < r.t_max)
            {
                (*hit).has_hit = true;
                (*hit).dist = t1;
                (*hit).position = r.origin + t1 * r.direction;
                (*hit).normal = normalize((*hit).position - center);
                return true;
            }
            var t2 = -b_half + sqrt(pow(b_half, 2) - c);
            if(t2 > r.t_min && t2 < r.t_max)
            {
                (*hit).has_hit = true;
                (*hit).dist = t2;
                (*hit).position = r.origin + t2 * r.direction;
                (*hit).normal = normalize((*hit).position - center);
                return true;
            }
            return false;
        }

        fn intersect_scene(r : ptr<function, Ray>, hit : ptr<function, HitInfo>) -> bool
        {
            const plane_onb = Onb(vec3f(-1.0, 0.0, 0.0), vec3f(0.0, 0.0, 1.0), vec3f(0.0, 1.0, 0.0));
            const plane_x0 = vec3f(0.0);
            if(intersect_plane((*r), hit, plane_x0, plane_onb.normal))
            {
                (*hit).color = vec3f(0.1, 0.7, 0.0);
                (*hit).shader = 1;
                (*hit).text_coords[0] = dot(((*hit).position - plane_x0), plane_onb.tangent);
                (*hit).text_coords[1] = dot(((*hit).position - plane_x0), plane_onb.binormal);
                (*hit).use_texture = true;
                (*r).t_max = (*hit).dist;
            }
            if(intersect_triangle((*r), hit, array<vec3f,3>(vec3f(-0.2, 0.1, 0.9), vec3f(0.2, 0.1, 0.9), vec3f(-0.2, 0.1, -0.1))))
            {
                (*hit).color = vec3f(0.4, 0.3, 0.2);
                (*hit).shader = 1;
                (*r).t_max = (*hit).dist;
                (*hit).use_texture = false;
            }
            if(intersect_sphere((*r), hit, vec3f(0.0, 0.5, 0.0), 0.3))
            {
                (*r).t_max = (*hit).dist;
                (*hit).color = vec3f(0.0, 0.0, 0.0);
                (*hit).shader = u32(uniforms.shader);
                (*hit).specular = 0.1;
                (*hit).shininess = 42;
                if(dot((*hit).normal, (*r).direction) < 0)
                {
                    (*hit).ior1_over_ior2 = 1/1.5;
                }
                else
                {
                    (*hit).ior1_over_ior2 = 1.5;
                    (*hit).normal = -(*hit).normal;
                }
                (*hit).use_texture = false;
            }
            
            return (*hit).has_hit;
        }

        fn sample_point_light(pos: vec3f) -> Light
        {
            var light : Light;
            var light_pos = vec3f(0.0, 1.0, 0.0);
            light.L_i = PI / dot(light_pos - pos, light_pos - pos);
            light.w_i = normalize(light_pos - pos);
            light.dist = length(light_pos - pos);
            return light;
        }

        fn lambertian(r: ptr<function,Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
            var light : Light = sample_point_light((*hit).position);
            var L_a = (*hit).color * 0.1;  //ambient light
            var ro = (*hit).color * 0.9;
            var L_r = ro * light.L_i * dot((*hit).normal, light.w_i) / PI;

            var shadow_ray = Ray((*hit).position, normalize(vec3f(0.0, 1.0, 0.0) - (*hit).position), 1.0e-4, light.dist);
            var shadow_hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 1, false, 0, 0, 0, 0, 0, vec2f(0.0), false);
            if(intersect_scene(&shadow_ray, &shadow_hit))
            {
                return L_a;
            }
            else
            {
                return L_r + L_a;
            }
        }

        fn reflection_shader(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
            (*hit).has_hit = false; 
            (*hit).depth = (*hit).depth + 1;
            (*r).origin = (*hit).position;
            (*r).t_min = 1.0e-4;
            (*r).t_max = 1.0e32;
            (*r).direction = reflect((*r).direction, (*hit).normal);
            return vec3f(0.0);
        }

        fn refraction_shader(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
            var dot_prod = dot(-(*r).direction, (*hit).normal);
            (*hit).cos_sq_t = 1 - pow((*hit).ior1_over_ior2, 2) * (1 - pow(dot_prod, 2));
            if((*hit).cos_sq_t < 0)
            {
                reflection_shader(r, hit);
            }
            else
            {
                (*r).direction = (*hit).ior1_over_ior2 * (dot_prod * (*hit).normal + (*r).direction) - (*hit).normal * sqrt((*hit).cos_sq_t);
                (*hit).has_hit = false; 
                (*hit).depth = (*hit).depth + 1;
                (*r).origin = (*hit).position;
                (*r).t_min = 1.0e-4;
                (*r).t_max = 1.0e32;
            }
            return vec3f(0.0);
        }

        fn phong_shader(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
            var dot_prod = dot(-(*r).direction, (*hit).normal);
            var light : Light = sample_point_light((*hit).position);
            var ro_d = 0.9 * (*hit).color;
            var ro_s = 0.1;
            var L_a = 0.1 * (*hit).color;
            var w_r = reflect((*r).direction, (*hit).normal);
            var L_r = (ro_d/PI + ro_s * pow(max(0,dot(w_r, light.w_i)),(*hit).shininess) *((*hit).shininess + 2)/(2 * PI)) * light.L_i * max(0,dot(-(*r).direction, (*hit).normal));
            
            var shadow_ray = Ray((*hit).position, normalize(vec3f(0.0, 1.0, 0.0) - (*hit).position), 1.0e-4, light.dist);
            var shadow_hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 1, false, 0, 0, 0, 0, 0, vec2f(0.0), false);
            if(intersect_scene(&shadow_ray, &shadow_hit))
            {
                return L_a;
            }
            else
            {
                return L_r + L_a;
            }
        }

        fn glossy_shader(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
            return phong_shader(r, hit) +  refraction_shader(r, hit);
        }

        fn shade(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
            switch (*hit).shader
            {
                case 0 {
                    return (*hit).color;
                }
                case 1{
                    return lambertian(r, hit);
                }
                case 2{
                    return reflection_shader(r, hit);
                }
                case 3{
                    return refraction_shader(r, hit);
                }
                case 4{
                    return phong_shader(r, hit);
                }
                case 5{
                    return glossy_shader(r,hit);
                }
                case default {return (*hit).color;}
            }
        }

        @fragment
        fn main_fs(@location(0) coords : vec2f) -> @location(0) vec4f
        {
            const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
            const max_depth = 10;
            var texture_result = vec3f(0.0);
            var texture_color = vec4f(0.0);
            var result = vec3f(0.0);
            var final_result = vec4f(0.0);
            var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 1, false, 0, 0, 0, 0, 0, vec2f(0.0), false);
            var num_subdivs = u32(pow(uniforms.subdivs, 2));
            for(var c: u32 = 0; c < num_subdivs; c++)
            {
                var uv = vec2f(jitter[c].x + coords.x*uniforms.aspect*0.5, jitter[c].y +coords.y*0.5);
                var r = get_camera_ray(uv);
                var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), 1, false, 0, 0, 0, 0, 0, vec2f(0.0), false);
                for(var i = 0; i < max_depth; i++)
                {
                    if(intersect_scene(&r, &hit)) 
                    {
                        var shade_result = shade(&r, &hit);
                        if(hit.use_texture)
                        {
                            texture_result += shade_result;
                        }
                        else
                        {
                            result += shade_result;
                        }
                    }
                    else { result += bgcolor.rgb; break;}
                    if(hit.has_hit) { break;}
                }
                texture_color += textureSample(my_texture, my_sampler, 0.02 * hit.text_coords);
            }
            final_result += vec4f(result / f32(num_subdivs), bgcolor.a) + (texture_color/f32(num_subdivs)) * vec4f(texture_result/f32(num_subdivs), 0.0);
            const gamma = 1.45;
            return vec4f(pow(final_result.rgb, vec3f(1.0/gamma)), bgcolor.a);
        }
    </script>
    <input type = "range" id = "sliderCamera" min = "0.1" max = "2.0" step = "0.01" value = "1.0"> 
    <select id = "textureMenu" size = "1">
        <option value = "clamp-to-edge"> Clamp-to-edge </option>
        <option value = "repeat" selected> Repeat </option>
    </select>
    <select id = "samplerMenu" size = "1">
        <option value = "nearest"> Nearest </option>
        <option value = "linear" selected> Linear </option>
    </select>
    <select id = "subdivsMenu" size = "1">
        <option value = "2.0"> 2 subdivisions </option>
        <option value = "4.0"> 4 subdivisions</option>
        <option value = "8.0"> 8 subdivisions</option>
        <option value = "16.0"> 16 subdivisions</option>
    </select>
    <select id = "shaderSphereMenu" size = "1">
        <option value = "0"> Base Color </option>
        <option value = "1"> Diffuse </option>
        <option value = "2"> Mirror </option>
        <option value = "3"> Refractive </option>
        <option value = "4"> Phong </option>
        <option value = "5"> Glossy </option>
</body>
</html> 